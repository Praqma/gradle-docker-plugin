plugins { id 'com.jfrog.bintray' version '0.6' }

apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

repositories { jcenter() }

targetCompatibility = sourceCompatibility = '1.6'

group = 'net.praqma.gradle'
version = '0.1'
description = 'Gradle plugin to orchestrate Docker containers'

ext {
	pluginId = 'net.praqma.docker'
	artifact = 'gradle-docker-plugin'
	github = 'https://github.com/Praqma/gradle-custom-dist-plugin'
}

dependencies {
	compile localGroovy()
	compile gradleApi()

	compile 'com.github.docker-java:docker-java:0.10.3'
	//compile 'com.github.docker-java:docker-java:0.10.4-SNAPSHOT'
}

task sourceJar(type: Jar) {
	classifier 'sources'
	from sourceSets.main.allSource
}

publishing {
	publications {
		plugin(MavenPublication) {
			artifactId artifact

			from components.java
			artifact sourceJar

			pom.withXml {
				asNode().appendNode('description', project.description)
			}
		}
	}
}

bintray {
	// E.g. defined in gradle.properties
	user = project.bintrayUser
	key = project.bintrayApiKey

	publications = ['plugin']
	dryRun = false //Whether to run this as dry-run, without deploying
	publish = true //If version should be auto published after an upload
	pkg {
		repo = 'gradle-plugins'
		userOrg = 'praqma'
		name = artifact
		desc = project.description
		websiteUrl = github
		vcsUrl = "${project.github}.git"
		licenses = ['Apache-2.0']
		publicDownloadNumbers = true
		//Optional version descriptor
		version {
			attributes = ['gradle-plugin': "${pluginId}:${group}:${artifact}"]
		}
	}
}

